pragma ComponentBehavior: Bound

import QtQuick
import QtQuick.Layouts
import QtQuick.Controls

import qs.Data
import qs.Helpers

Slider {
    id: root

    hoverEnabled: true
    Layout.alignment: Qt.AlignHCenter

    property bool dotEnd: true
    property real trackHeightDiff: 15
    property real handleGap: 6
    property real trackDotSize: 4
    property bool useAnim: true
    property int valueWidth
    property int valueHeight
    property string icon
    property int iconSize

    implicitWidth: valueWidth || 200
    implicitHeight: valueHeight || 40

    readonly property real availableTrackWidth: availableWidth - handleGap * 2
    readonly property real trackHeight: height - trackHeightDiff
    readonly property real handleWidth: pressed ? 2 : 4
    readonly property int dotCount: stepSize > 0 ? Math.floor((to - from) / stepSize) + 1 : 0

    component TrackDot: Rectangle {
        required property int index

        readonly property real stepValue: root.from + (index * root.stepSize)
        readonly property real normalizedValue: (stepValue - root.from) / (root.to - root.from)
        readonly property bool isActive: normalizedValue <= root.visualPosition

        anchors.verticalCenter: parent.verticalCenter
        x: root.handleGap + (normalizedValue * root.availableTrackWidth) - root.trackDotSize / 2

        width: root.trackDotSize
        height: root.trackDotSize
        radius: Appearance.rounding.normal
        visible: root.dotEnd && index > 0 && index < root.dotCount - 1
        color: isActive ? Themes.colors.on_primary : Themes.colors.on_secondary_container
    }

    MArea {
        anchors.fill: parent
        onPressed: mouse => mouse.accepted = false
        cursorShape: root.pressed ? Qt.ClosedHandCursor : Qt.PointingHandCursor
    }

    background: Item {
        implicitWidth: root.valueWidth || 200
        implicitHeight: root.valueHeight || 40
        width: root.availableWidth
        height: root.availableHeight
        x: root.leftPadding
        y: root.topPadding

        Loader {
            active: root.icon !== ""
            anchors {
                left: parent.left
                leftMargin: 10
                verticalCenter: parent.verticalCenter
            }
            sourceComponent: MatIcon {
                icon: root.icon
                color: Themes.colors.on_primary
                font.pixelSize: root.iconSize || Appearance.fonts.medium
            }
        }

        StyledRect {
            anchors {
                verticalCenter: parent.verticalCenter
                left: parent.left
            }
            width: root.handleGap + (root.visualPosition * root.availableTrackWidth) - (root.handleWidth / 2 + root.handleGap)
            height: root.trackHeight
            color: Themes.colors.primary
            radius: Appearance.rounding.normal

            topRightRadius: Appearance.rounding.small * 0.5
            bottomRightRadius: Appearance.rounding.small * 0.5
        }

        StyledRect {
            anchors {
                verticalCenter: parent.verticalCenter
                right: parent.right
            }
            width: root.handleGap + ((1 - root.visualPosition) * root.availableTrackWidth) - (root.handleWidth / 2 + root.handleGap)
            height: root.trackHeight
            color: Themes.colors.surface_container_highest
            radius: Appearance.rounding.normal

            topLeftRadius: Appearance.rounding.small * 0.5
            bottomLeftRadius: Appearance.rounding.small * 0.5
        }

        Repeater {
            model: root.dotCount
            TrackDot {
                required property int modelData
                index: modelData
            }
        }
    }

    handle: StyledRect {
        width: root.handleWidth
        height: root.height
        x: root.handleGap + (root.visualPosition * root.availableTrackWidth) - width / 2
        anchors.verticalCenter: parent.verticalCenter
        radius: Appearance.rounding.normal
        color: Themes.colors.primary

        Behavior on width {
            NumberAnimation {
                duration: Appearance.animations.durations.small
                easing.type: Easing.OutCubic
            }
        }
    }
}
